

-- =============================================
-- Author     :	DELPIANO	
-- Create date: 09-02-2014
-- Description:	RICERCA BARCODE CON GIACENZA PER UNA CATEGORIA O UN EAN
-- Parametri  : @ID_LINGUA --> codice lingua richiesta : per zero utilizza quella di default
-- Restituisce: Elenco barcode con prezzo e foto  
-- =============================================
CREATE PROCEDURE [dbo].[PRC_GET_BARCODE_CATEGORIA_CON_GIACENZA]
@ID_CATEGORIA		INT,
@ID_LINGUA			INT =0,
@EAN				VARCHAR(13) = '',
@ID_ARTICOLO		INT=0,
@CODICE_ARTICOLO	VARCHAR(20) = '',
@STATUS				VARCHAR(10) = '',
@CODICE_NEGOZIO		VARCHAR(10) = ''
AS
BEGIN

	SET NOCOUNT ON;

	
	SELECT 
	G.EAN,
	A.ID_ARTICOLO,
	A.CODICE_ARTICOLO,
	A.NOME,
	A.NOME_BREVE,
	A.DESCRIZIONE,
	A.CODICE_IVA,
	ISNULL(P.PREZZO, PREZZO_BASE) AS PREZZO,
	ISNULL(P.FOTO, DEFAULT_IMAGE) AS FOTO,
	G.GIACENZA,
	A.ID_CATEGORIA,
	A.CODICE_UM, 
	A.CODICE_ARTICOLO_PRINCIPALE,
	A.QTA_ARTICOLO_PRINCIPALE,
	A.LOTTO_VENDITA,
	A.REPARTO_VENDITA,
	--A.CODICE_UM_ARTICOLO_PRINCIPALE 
	CASE WHEN A.TIPO_ARTICOLO = 'S' THEN A.CODICE_UM ELSE A.CODICE_UM_ARTICOLO_PRINCIPALE END AS CODICE_UM_ARTICOLO_PRINCIPALE 
	INTO #RISULTATO
	FROM 
	GIACENZA G WITH (NOLOCK)
	JOIN ARTICOLO_old       A WITH (NOLOCK) ON G.ID_ARTICOLO = A.ID_ARTICOLO
	LEFT JOIN PREZZO P WITH (NOLOCK) ON G.CODICE_NEGOZIO = P.CODICE_NEGOZIO AND G.EAN = P.EAN
	WHERE
	G.CODICE_NEGOZIO = CASE WHEN @CODICE_NEGOZIO = '' THEN G.CODICE_NEGOZIO ELSE @CODICE_NEGOZIO END AND
	A.STATUS		=	CASE WHEN @STATUS = '' THEN A.STATUS ELSE @STATUS  END  AND
	A.ID_CATEGORIA	=	CASE WHEN @ID_CATEGORIA = 0 THEN A.ID_CATEGORIA ELSE @ID_CATEGORIA END AND
	G.GIACENZA		>	0 AND
	G.EAN			=	CASE WHEN @EAN = '' THEN G.EAN ELSE @EAN END AND
	A.CODICE_ARTICOLO	=	CASE WHEN @CODICE_ARTICOLO = '' THEN A.CODICE_ARTICOLO ELSE @CODICE_ARTICOLO  END  AND
	A.ID_ARTICOLO	=	CASE WHEN @ID_ARTICOLO = 0 THEN A.ID_ARTICOLO ELSE @ID_ARTICOLO  END  

	IF @ID_LINGUA <> 0
	BEGIN
			UPDATE #RISULTATO 
					SET #RISULTATO.DESCRIZIONE = ARTICOLO_LINGUA.DESCRIZIONE
			FROM
					#RISULTATO  JOIN ARTICOLO_LINGUA  
					ON #RISULTATO.ID_ARTICOLO = ARTICOLO_LINGUA.ID_ARTICOLO AND ID_LINGUA = @ID_LINGUA
	END


	SELECT * FROM #RISULTATO JOIN CATEGORIA_old CATEGORIA ON 
		#RISULTATO.ID_CATEGORIA = CATEGORIA.ID_CATEGORIA 
		WHERE CATEGORIA.VISUALIZZA = 'S'
	ORDER BY ORDINE, NOME

END