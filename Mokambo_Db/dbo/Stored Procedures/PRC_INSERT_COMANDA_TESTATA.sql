
CREATE PROCEDURE [dbo].[PRC_INSERT_COMANDA_TESTATA]
@ID_BASKET			  INT,
@ROWS				  INT OUTPUT,
@RISULTATO			  CHAR(2) = '' OUTPUT,
@DESRISULTATO		  VARCHAR(1000)	= '' OUTPUT
AS
SET NOCOUNT ON
BEGIN TRY
	
	IF EXISTS (SELECT 1 FROM BASKET_RIGA WHERE ID_BASKET = @ID_BASKET AND QTA <> QTA_INVIATA)
	BEGIN
		INSERT INTO [COMANDA_TESTATA]
				(ID_BASKET
				,ID_REPARTO
				,DESCRIZIONE
				,STAMPANTE
				,DATA_CREAZIONE
				,EVASA
				,TAVOLO
				,CODICE_UTENTE)
	 
		select  max(t.ID_BASKET) as ID_BASKET, a.ID_REPARTO, '', NOMESTAMPANTECOMANDA as stampante, getdate(), 0, TAVOLO, ''
			from [dbo].[BASKET_TESTATA] t
			join [dbo].[BASKET_RIGA] r on t.ID_BASKET = r.ID_BASKET
			left join [dbo].[ARTICOLO] a on a.ID_ARTICOLO = r.ID_ARTICOLO
			left join [dbo].[REPARTO] rp on rp.IDREPARTO = a.ID_REPARTO
			where t.ID_BASKET = @ID_BASKET AND r.QTA_INVIATA <> r.QTA
			group by a.ID_REPARTO, NOMESTAMPANTECOMANDA, TAVOLO

		SET @ROWS = @@ROWCOUNT
		SET @RISULTATO = 'OK'
		SET @DESRISULTATO = 'INSERIMENTO EFFETTUATO CORRETTAMENTE'
	END
	ELSE
	BEGIN
		SET @RISULTATO = 'KO'
		SET @DESRISULTATO = 'NESSUNA VARIAZIONE AL BASKET'
	END
		
END TRY
BEGIN CATCH
	SET @RISULTATO = 'KO'
	SET @DESRISULTATO = ERROR_MESSAGE()
END CATCH