
CREATE PROCEDURE [dbo].[PRC_INSERT_BASKET_TESTATA]
@CASSA 				  INT,
@CODICE_NEGOZIO	      VARCHAR(10),
@EVASO				  BIT,
@STAMPATO			  BIT,
@PAGATO				  BIT,
@TOTALE				  MONEY,
@CODICE_UTENTE		  VARCHAR(30),
@TAVOLO				  INT,
@DESC_ORDINAZIONE	  VARCHAR(250),
@ID_BASKET			  INT,
@ID_BASKET_OUT		  INT OUTPUT,
@RISULTATO			  CHAR(2) = '' OUTPUT,
@DESRISULTATO		  VARCHAR(1000)	= '' OUTPUT
AS
SET NOCOUNT ON
BEGIN TRY
	
	DECLARE @BASKET TABLE (ID_BASKET INT)

	MERGE INTO BASKET_TESTATA AS TARGET
	USING (SELECT @ID_BASKET, @CASSA, @CODICE_NEGOZIO, @EVASO, @STAMPATO, @PAGATO, @TOTALE,	@CODICE_UTENTE,	@TAVOLO, @DESC_ORDINAZIONE)
		  AS SOURCE(ID_BASKET, CASSA, CODICE_NEGOZIO, EVASO, STAMPATO, PAGATO, TOTALE, CODICE_UTENTE, TAVOLO, DESC_ORDINAZIONE)
	ON SOURCE.ID_BASKET = TARGET.ID_BASKET
	WHEN MATCHED THEN UPDATE SET CASSA = SOURCE.CASSA,
								 CODICE_NEGOZIO = SOURCE.CODICE_NEGOZIO,
								 EVASO = SOURCE.EVASO,
								 STAMPATO = SOURCE.STAMPATO,
								 PAGATO = SOURCE.PAGATO,
								 TOTALE = SOURCE.TOTALE,
								 CODICE_UTENTE = SOURCE.CODICE_UTENTE,
								 TAVOLO = SOURCE.TAVOLO,
								 DESC_ORDINAZIONE = SOURCE.DESC_ORDINAZIONE
	WHEN NOT MATCHED THEN INSERT (DATA_CREAZIONE, CASSA, CODICE_NEGOZIO, EVASO, STAMPATO, PAGATO, TOTALE, CODICE_UTENTE, TAVOLO, DESC_ORDINAZIONE)
						  VALUES (GETDATE(), SOURCE.CASSA, SOURCE.CODICE_NEGOZIO, SOURCE.EVASO, SOURCE.STAMPATO, SOURCE.PAGATO, SOURCE.TOTALE, SOURCE.CODICE_UTENTE, SOURCE.TAVOLO, SOURCE.DESC_ORDINAZIONE)
	OUTPUT inserted.ID_BASKET INTO @BASKET(ID_BASKET);
	
	SELECT @ID_BASKET_OUT = ID_BASKET FROM @BASKET

	SET @RISULTATO = 'OK'
	SET @DESRISULTATO = 'INSERIMENTO EFFETTUATO CORRETTAMENTE'
		
END TRY
BEGIN CATCH
	SET @RISULTATO = 'KO'
	SET @DESRISULTATO = ERROR_MESSAGE()
END CATCH